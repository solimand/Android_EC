grammar org.project.droolsDSL.Ddsl with org.eclipse.xtext.common.Terminals

generate ddsl "http://www.project.org/droolsDSL/Ddsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


DroolsModel:
	statements += Statement*
;

Statement:
	'on' event=Event 'set' fluent+=Fluent (',' fluent+=Fluent)* ';' 	
;

Event:
	eventName=ID	
	('('param+=EventFeature (',' param+=EventFeature)* ')')?	
;

Fluent:
	name=ID
	('in' timePart = InRule)?
	('to' valuePart = ToRule)
	('if' condPart = ConditionRule)?
;

ConditionRule:
	({Expression} condition = Expression)
;

ToRule:
	({Expression} expression = PlusOrMinus)
;

InRule:	
//	add Time Expression
	({InExpr} inTimeExpr=InTimeExpression)	
;

EventFeature: name=ID;

// LEFT_ASSOCIATIVITY
Expression: Or;

Or returns Expression:
	And ({Or.left=current} "OR" right=And)* 
;

And returns Expression:
	Equality ({And.left=current} "AND" right=Equality)* 
;

Equality returns Expression:
	Comparison (
		{Equality.left=current} op=("=="|"!=")
		right=Comparison
	)* 
;

Comparison returns Expression:
	PlusOrMinus (
		{Comparison.left=current} op=(">="|"<="|">"|"<")
		right=PlusOrMinus
	)* 
;

PlusOrMinus returns Expression:
	MulOrDiv (
	({Plus.left=current} '+' | {Minus.left=current} '-')
	right=MulOrDiv
	)* 
;

MulOrDiv returns Expression:
	Primary (
		{MulOrDiv.left=current} op=('*'|'/'|'%'|'^')
		right=Primary
	)*
;

Primary returns Expression:
	'(' Expression ')' |
	{Not} "!" expression=Primary |
	Atomic
;

Atomic returns Expression:
	{FloatConstant} value=FLOAT |
	{IntConstant} value=INT |
	{BoolConstant} value=('true'|'false')
	|{Reference} ref=[ReferenceType|ID] ('at' fluentValueSample = AtTimePrimary)?
//	|{CurrentTime} value='now'
;
ReferenceType:EventFeature|Fluent;

AtExpr:	AtTimeExpression;
AtTimeExpression returns AtExpr:
	AtTimePrimary(
		{AtTimePlusOrMin.left=current} op=('-'|'+')
		right=AtTimePrimary
	)* 
;
AtTimePrimary returns AtExpr:
	'(' AtTimeExpression ')' |
	AtTimeAtomic
;
AtTimeAtomic returns AtExpr:
	{AtTimeIntConstant} /*('+'|'-')?*/ value= INT
	|{AtTimeCurrentTime} atTimeValue='now'
//	|long
;

InExpr:	InTimeExpression;
InTimeExpression returns InExpr:
	InTimePrimary(
		{InTimePlusOrMin.left=current} op=('-'|'+')
		right=InTimePrimary
	)* 
;
InTimePrimary returns InExpr:
	'(' InExpr ')' |
	InTimeAtomic
;
InTimeAtomic returns InExpr:
	{InTimeIntConstant} /*('+'|'-')?*/ value= INT
	|{InTimeCurrentTime} inTimeValue='now'
//	|long
;

terminal FLOAT returns ecore::EFloat: ('0'..'9')*'.'('0'..'9')+;